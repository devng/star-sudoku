apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'

task wrapper(type: Wrapper) {
	gradleVersion = '2.5'
}

ext {
    mainClassName = "StarSUDOKUMain"
}

// Keep this in sync with GV.VERSION
version ='1.0'

repositories {
    mavenCentral()
}

dependencies {
    compile 'itext:itext:1.3.1'

    testCompile group: "org.testng", name: "testng", version: "6.14.3"
}

sourceCompatibility = 1.7
targetCompatibility = 1.7

jar {
    manifest {
        attributes 'Implementation-Title': 'StarSUDOKU',
                'Implementation-Version': version,
                'Main-Class': "${mainClassName}"
    }

    // also include other jar dependencies
    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

// Windows wrapper configuration to generate windows exe file with Launch4J
task launch4jConfig(type: Copy) {
    from 'src/main/resources/launch4j/launch4j.xml'
    into 'build/launch4j'
    expand(
            JAR_FILE: project.jar.archivePath,
            VERSION: project.version,
            ICON: file('src/main/resources/launch4j/sudoku-icon.ico')
    )
}

task launch4j(type: Exec, dependsOn: [':jar', ':launch4jConfig']) {
    def launch4jCfg = file('build/launch4j/launch4j.xml')
    def isWindows = System.properties['os.name'].startsWith('Windows')
    def launch4jDir = System.getenv('LAUNCH4J_HOME')

    if (launch4jDir == null || !(new File(launch4jDir).exists())) {
        print "WARN: 'LAUNCH4J_HOME' not defined or invalid. Launch4j (http://launch4j.sourceforge.net) is required to generare the StarSUDOKU Windows EXE file."
        return
    }

    if (isWindows) {
        commandLine 'cmd', '/c', launch4jDir + "/launch4j.exe", launch4jCfg
    } else {
        commandLine launch4jDir + "/launch4j", launch4jCfg
    }
}
