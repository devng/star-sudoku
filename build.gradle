apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'

task wrapper(type: Wrapper) {
	gradleVersion = '2.4.0'
}

// Keep this in sync with GV.VERSION
version ='1.0'

repositories { 
	mavenCentral() 
}

dependencies {
    compile 'itext:itext:1.3.1'
}

sourceCompatibility = 1.7
targetCompatibility = 1.7

jar {
    manifest {
        attributes 'Implementation-Title': 'StarSUDOKU',
                'Implementation-Version': version,
                'Main-Class': 'StarSUDOKUMain'
    }

    // also include other jar dependencies
    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

// Windows wrapper configuration to generate windows exe file with Launch4J
task launch4jConfig(type: Copy) {
    from 'src/main/resources/launch4j.xml'
    into 'build/launch4j'
    expand(
            JAR_FILE: project.jar.archivePath,
            VERSION: project.version,
            ICON: file('src/main/resources/sudoku-icon.ico')
    )
}

build.dependsOn 'launch4jConfig'
